plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.epages.restdocs-api-spec' version '0.19.4'
}

group = 'maskun'
version = '1.1.0'
description = 'quiet-chatter'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jspecify:jspecify:1.0.0'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    implementation 'org.flywaydb:flyway-database-postgresql'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation 'org.springframework.security:spring-security-test'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'com.redis:testcontainers-redis:2.2.4'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.4'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'net.datafaker:datafaker:2.5.3'
    testImplementation 'org.instancio:instancio-junit:5.5.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    annotationProcessor("org.projectlombok:lombok")
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test', Test) {
    useJUnitPlatform()
    useJUnitPlatform {
        excludeTags("external-api")
    }
    environment("TESTCONTAINERS_REUSE_ENABLE", "true")
}

tasks.register("testExternalApi", Test) {
    group = "verification"
    description = "외부 API 테스트"

    useJUnitPlatform {
        includeTags("external-api")
    }

    environment("TESTCONTAINERS_REUSE_ENABLE", "true")
}

// --- API 문서화 및 테스트 설정 (API Documentation & Test Setup) ---

// 1. 문서 생성 전용 테스트 태스크 등록: 'restdocs' 태그가 붙은 테스트만 골라서 실행
tasks.register("testDocs", Test) {
    group = "verification"
    description = "API 문서 생성을 위한 테스트 실행"
    useJUnitPlatform {
        includeTags("restdocs")
    }
}

// 2. OpenAPI 3.0 스펙 생성 설정 (restdocs-api-spec 플러그인)
openapi3 {
    server = 'http://localhost:8080'
    title = 'Quiet Chatter API'
    description = 'Quiet Chatter API documentation'
    version = '1.1.0'
    format = 'json'
    outputDirectory = 'build/api-spec'
}

// 3. 태스크 의존성 연결: openapi3가 실행되기 전에 반드시 testDocs가 먼저 실행되어 스니펫을 만들어야 함
tasks.matching { it.name == 'openapi3' }.configureEach {
    dependsOn 'testDocs'
}

// 4. 배포용 JAR 패키징 설정: 빌드 시 문서를 생성하고, JAR 파일 내 static/docs 경로에 포함시킴
tasks.named('bootJar') {
    dependsOn 'openapi3'
    from('build/api-spec') {
        into 'static/docs'
    }
}
